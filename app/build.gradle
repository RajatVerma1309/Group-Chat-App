import java.nio.file.Files
import java.nio.file.Paths

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.2.2"
        classpath "com.google.gms:google-services:4.3.8"
        classpath "com.google.firebase:firebase-crashlytics-gradle:2.7.0"
    }
}

apply plugin: "com.android.application"

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "smoke-test-rules.pro"
            testProguardFiles "infrastructure-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    if (project.hasProperty("testBuildType")) {
        testBuildType project.getProperty("testBuildType")
    }
}



dependencies {
    // Firebase
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-annotations"
    implementation "com.google.firebase:firebase-appindexing"
    implementation "com.google.firebase:firebase-auth"
    implementation "com.google.firebase:firebase-common"
    implementation "com.google.firebase:firebase-config"
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-database"
    implementation "com.google.firebase:firebase-dynamic-links"
    implementation "com.google.firebase:firebase-firestore"
    implementation "com.google.firebase:firebase-functions"
    implementation "com.google.firebase:firebase-inappmessaging"
    // TODO(yifany): Restore after messaging in github is up to date
    // implementation "com.google.firebase:firebase-messaging"
    implementation "com.google.firebase:firebase-ml-modeldownloader"
    implementation "com.google.firebase:firebase-perf"
    implementation "com.google.firebase:firebase-storage"

    // Common utilities (application side)
    implementation "androidx.test:rules:1.4.0"
    implementation "androidx.test:runner:1.4.0"
    implementation "com.google.truth:truth:1.0.1"
    implementation "junit:junit:4.13.2"
    implementation "androidx.test:core:1.4.0"
    implementation 'androidx.appcompat:appcompat:1.3.0'

    // Common utilities (instrumentation side)
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "junit:junit:4.13.2"
}

clean.doLast {
    def paths = Files.newDirectoryStream(Paths.get("."), "build-*")

    for (Path path : paths) {
        project.delete "$path/"
    }
}

apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"